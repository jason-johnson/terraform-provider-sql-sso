---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "sqlsso_postgresql_server_aad_account Resource - terraform-provider-sqlsso"
subcategory: ""
description: |-
  sqlsso_postgresql_server_aad_account enables AAD authentication for an Azure Postgresql Flexible.
---

# sqlsso_postgresql_server_aad_account (Resource)

`sqlsso_postgresql_server_aad_account` enables AAD authentication for an Azure Postgresql Flexible.

## Example Usage

```terraform
provider "azurerm" {
  features {}
}

provider "sqlsso" {}

data "azurerm_client_config" "current" {}

resource "azurerm_resource_group" "example" {
  name     = "example-resources"
  location = "West Europe"
}

resource "azurerm_postgresql_flexible_server" "example" {
  name                          = "example-postgresql-server"
  resource_group_name           = azurerm_resource_group.example.name
  location                      = azurerm_resource_group.example.location
  version                       = "16"
  public_network_access_enabled = true
  authentication {
    active_directory_auth_enabled = true
    password_auth_enabled         = false
    tenant_id                     = data.azurerm_client_config.current.tenant_id
  }

  sku_name = "B_Standard_B1ms"

  lifecycle {
    ignore_changes = [
      zone
    ]
  }
}

resource "azurerm_postgresql_flexible_server_active_directory_administrator" "example" {
  server_name         = azurerm_postgresql_flexible_server.example.name
  resource_group_name = azurerm_resource_group.example.name
  tenant_id           = data.azurerm_client_config.current.tenant_id
  object_id           = data.azurerm_client_config.current.object_id
  principal_name      = data.azurerm_client_config.current.display_name
  principal_type      = "User"
}

resource "azurerm_postgresql_flexible_server_database" "example" {
  name      = "example-db"
  server_id = azurerm_postgresql_flexible_server.example.id
  collation = "en_US.utf8"
  charset   = "utf8"
}

resource "azurerm_service_plan" "example" {
  name                = "example-plan"
  resource_group_name = azurerm_resource_group.example.name
  location            = azurerm_resource_group.example.location
  os_type             = "Linux"
  sku_name            = "P1v2"
}

resource "azurerm_linux_web_app" "example" {
  name                = "example-linux-web-app"
  resource_group_name = azurerm_resource_group.example.name
  location            = azurerm_service_plan.example.location
  service_plan_id     = azurerm_service_plan.example.id

  site_config {}

  identity {
    type = "SystemAssigned"
  }
}

# This will require the right permissions, see azuread_service_principal
data "azuread_service_principal" "example" {
  object_id = azurerm_linux_web_app.example.identity[0].principal_id
}

resource "sqlsso_postgresql_server_aad_account" "example" {
  sql_server_dns = azurerm_postgresql_flexible_server.example.fully_qualified_domain_name
  database       = azurerm_postgresql_flexible_server_database.example.name
  user_name      = data.azurerm_client_config.current.display_name
  account_name   = azurerm_linux_web_app.example.name
  role           = "owner"
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `account_name` (String) The name of the account to add to the database.
- `database` (String) The name of the database to add the account.
- `sql_server_dns` (String) The DNS name of the SQL server to add the account.
- `user_name` (String) The name of the account that will log into the database (not currently infered from connection).

### Optional

- `port` (Number) Port to connect to the database server.
- `role` (String) The role the account should get (e.g. owner, reader, etc.).

### Read-Only

- `id` (String) The ID of this resource.
